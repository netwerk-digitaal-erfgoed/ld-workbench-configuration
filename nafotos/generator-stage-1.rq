PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc:      <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX edm:     <http://www.europeana.eu/schemas/edm/>
PREFIX ore:     <http://www.openarchives.org/ore/terms/>
PREFIX nao:     <https://archief.nl/def/ontologie/>
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>
PREFIX odrl:    <http://www.w3.org/ns/odrl/2/>
PREFIX doap:    <http://usefulinc.com/ns/doap#>
PREFIX svcs:    <http://rdfs.org/sioc/services#>

CONSTRUCT {

	# uri_cho
	?Foto a edm:ProvidedCHO ;
		edm:type ?edm_type ;
		dc:creator ?creator ;
# 		dc:publisher ?publisher ; ???
        dc:description ?description ;
        dc:type ?genre ;
        dc:subject ?subject ;
        dcterms:spatial ?spatial ;
        dcterms:temporal ?temporal ;
        dcterms:medium ?material ;
 		dc:rights ?policy ;
        dc:identifier ?identifier ;
        dc:date ?date ;
        dcterms:isPartOf ?is_part_of .

	$this a ore:Aggregation ;
		edm:aggregatedCHO ?Foto ;
		edm:provider ?provider ;
		edm:dataProvider ?dataProvider ;
		edm:rights ?rights ;
		edm:isShownBy ?isShownBy ;
		edm:isShownAt ?isShownAt .

	?isShownBy a edm:WebResource ;
		svcs:has_service ?uri_iiif .

	?uri_iiif a svcs:Service ;
		dcterms:conformsTo <http://iiif.io/api/image> ;
		doap:implements <http://iiif.io/api/image/2/level1.json> .

} WHERE {

	# ?uri_ore = ?Fotobeschrijving
	# ?uri_cho = ?Foto

	# Fixed values ...
	BIND(("IMAGE") as ?edm_type)
	BIND(("Foto") as ?genre)
	BIND(("DC4EU") as ?provider)
	BIND( STRLANG("Nationaal Archief","nl") as ?dataProvider)

	# iterator levert Fotorecords op
	$this dc:identifier ?identifier ;
		  dcterms:creator/skos:prefLabel ?_creator .
	BIND(STRLANG(?_creator, "nl") AS ?creator)

	$this edm:isShownAt ?isShownAt .
	$this edm:hasView ?Zichtexemplaar .
	?Zichtexemplaar nao:webserviceIIIF ?iiif_info .
	BIND( IRI(CONCAT(REPLACE(STR(?iiif_info),"info.json","full/max/0/default.jpg"))) as ?isShownBy)
	BIND( IRI(CONCAT(REPLACE(STR(?iiif_info),"info.json",""))) as ?uri_iiif)

	$this ore:aggregates ?Foto .
	?Foto a nao:Archiefeenheid .

	?Fotobeschrijving foaf:primaryTopic ?Foto .

	OPTIONAL { ?Foto nao:isMateriaaltype ?_material . BIND(STRLANG(?_material, "nl") AS ?material) }

	# License "conversion" (via "filter" en bind)
	# <https://archief.nl/def/Policy/set_b_rechtenvrij__publiek_domein> > <http://creativecommons.org/publicdomain/mark/1.0/>
	BIND(IRI("http://creativecommons.org/publicdomain/mark/1.0/") as ?policy)
	?Fotobeschrijving odrl:hasPolicy <https://archief.nl/def/Policy/set_b_rechtenvrij__publiek_domein> .

	OPTIONAL { ?Fotobeschrijving dcterms:subject/skos:prefLabel ?_subject . BIND(STRLANG(?_subject, "nl") AS ?subject) }
	OPTIONAL { ?Fotobeschrijving nao:trefwoordAlgemeen ?_subject . BIND(STRLANG(?_subject, "nl") AS ?subject) }
	OPTIONAL { ?Fotobeschrijving nao:trefwoordLocatie ?_spatial . BIND(STRLANG(?_spatial, "nl") AS ?spatial) }
	OPTIONAL { ?Fotobeschrijving dc:coverage ?temporal  }
	OPTIONAL { ?Fotobeschrijving dcterms:date ?date  }
	OPTIONAL { ?Fotobeschrijving nao:titelFotocollectie ?_is_part_of . BIND(STRLANG(?_is_part_of, "nl") AS ?is_part_of) }
	OPTIONAL { ?Fotobeschrijving nao:titelFotoserie ?_is_part_of . BIND(STRLANG(?_is_part_of, "nl") AS ?is_part_of)  }
	OPTIONAL { ?Fotobeschrijving dc:description ?_description . BIND(STRLANG(?_description, "nl") AS ?description) }
	OPTIONAL { ?Fotobeschrijving nao:toelichting ?_description . BIND(STRLANG(?_description, "nl") AS ?description)  }
}
LIMIT 1000
